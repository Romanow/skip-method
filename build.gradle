plugins {
    id "idea"
    id "jacoco"
    id "maven-publish"
    id "org.jreleaser" version "1.19.0"
    id "org.jetbrains.kotlin.jvm" version "1.9.25"
    id "org.jlleitschuh.gradle.ktlint" version "12.1.1"
}

[compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "17"

ext {
    junitVersion = "5.11.0"
    mockitoVersion = "5.13.0"
    assertjVersion = "3.27.4"
    aspectjVersion = "1.9.22.1"
    lombokVersion = "1.18.34"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

ktlint {
    version.set("1.1.1")
}

repositories {
    mavenCentral()
}

configurations {
    aspectjWeaverAgent
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    aspectjWeaverAgent "org.aspectj:aspectjweaver:$aspectjVersion"
    implementation "org.aspectj:aspectjrt:$aspectjVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
}

test {
    useJUnitPlatform()
    systemProperty "skipOn", findProperty("skipOn")

    testLogging {
        showStandardStreams = true
    }

    doFirst {
        jvmArgs "--add-opens",
                "java.base/java.lang=ALL-UNNAMED",
                "-javaagent:${configurations.aspectjWeaverAgent.singleFile}"
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = "skip-method"
                description = "Skip method in test"
                url = "https://github.com/Romanow/skip-method/"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://opensource.org/license/mit"
                    }
                }
                developers {
                    developer {
                        id = "Romanow"
                        name = "Romanov Alexey"
                        email = "romanow.tech@gmail.com"
                    }
                }
                scm {
                    url = "https://github.com/Romanow/skip-method/"
                    connection = "scm:git:https://github.com/Romanow/skip-method.git"
                    developerConnection = "scm:git:ssh://github.com/Romanow/skip-method.git"
                }
            }
        }
    }
    repositories {
        maven {
            url = "build/staging-deploy"
        }
    }
}

jreleaser {
    signing {
        active = "ALWAYS"
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = "ALWAYS"
                    url = "https://central.sonatype.com/api/v1/publisher"
                    stagingRepository("build/staging-deploy")
                }
            }
        }
    }
}
