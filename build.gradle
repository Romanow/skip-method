plugins {
    id "idea"
    id "jacoco"
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "1.9.22"
    id "org.jlleitschuh.gradle.ktlint" version "12.1.1"
}

[compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "17"

ext {
    junitVersion = "5.11.0"
    mockitoVersion = "3.5.11"
    assertjVersion = "3.19.0"
    aspectjVersion = "1.9.20"
    lombokVersion = "1.18.16"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

ktlint {
    version.set("1.1.1")
}

repositories {
    mavenCentral()
}

configurations {
    aspectjWeaverAgent
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    aspectjWeaverAgent "org.aspectj:aspectjweaver:$aspectjVersion"
    implementation "org.aspectj:aspectjrt:$aspectjVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
}

test {
    useJUnitPlatform()
    systemProperty "skipOn", findProperty("skipOn")

    doFirst {
        jvmArgs "--add-opens",
                "java.base/java.lang=ALL-UNNAMED",
                "-javaagent:${configurations.aspectjWeaverAgent.singleFile}"
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = "playwright-page-object"
                description = "Page Object pattern for Playwright framework"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://opensource.org/license/mit"
                    }
                }
                developers {
                    developer {
                        name = "Romanov Alex"
                        email = "romanowalex@mail.ru"
                    }
                }
                scm {
                    url = "https://github.com/Romanow/playwright-page-object/"
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/Romanow/playwright-page-object")
            credentials {
                username = System.getenv("GITHUB_USER")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
